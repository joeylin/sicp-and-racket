# P40-P74

## 1.2 过程和过程所产生的进程(Procedures and the Processes They Generate)

至此，通过一些数学运算的实例，我们已经初步了解了 1.1 中提到的程序的三种基本元素：

1. 基本算数操作符，如 `+ -` 和数字等；
2. 将这些操作组合成复合操作，如复杂运算`(+ 1 (- 2 3))`；
3. 通过抽象的方法将这些复合操作定义为复合过程，从而当做单独的个体被命名、操作。

> The ability to visualize the consequences of the actions under consideration is crucial to becoming an expert programmer.

如一开始所定义的，过程是计算过程(computational process)在局部范围内演化的一种描述，它描述了计算过程是如何在先前阶段的基础上一步一步向前演化。

本小节主要通过视觉“形状”的方式展示简单的过程所产生的计算过程是如何演化的，以及在计算过程中所消耗的时间、空间资源。

### 1.2.1 线性递归与循环（迭代）

    #lang racket
    (define (fact n)
      (if (= n 1)
          1
          (* n (fact (- n 1)))))
    
    (fact 6)
    ;=> 720
    (define (fact-iter prod counter n)
      (if (> counter n)
          prod
          (fact-iter (* prod counter) (+ counter 1) n)))
    (define (fact-2 n)
      (fact-iter 1 1 n))
    (fact-2 6) 
    ;=> 720

第一种计算方法，先是扩展成一系列被延迟执行的操作（这里是乘法操作），然后真正计算的过程则是从展开的最终结果逐步缩减直至得出计算结果，这一类计算过程，称为**递归过程(recursive process)**，其中计算步骤的长度与参数`n`的大小线性相关，这类过程称为**线性递归过程(linear cursive process)**。

第二种计算方法，在每一步计算过程中，（通过参数传递）保存了`prod counter n`这些状态变量，这种则称为**迭代计算过程(iterative process)**，同样计算步骤的长度如果与参数`n`线性相关，则称为**线性迭代过程(linear iterative process)**。

> 当心递归计算过程(recursive process)与递归过程(recursive procedure)的区别。

> 这其实是从第一小节开始就应该注意到的一个问题，process 与 procedure 的区别，它们的意思相近，但是在本书的语境中需要区分开来。

> Process 更偏向于抽象的概念，它代表发生在背后的**计算过程**，它描绘的是发生在计算机中对其它一些抽象信息如数据的操作过程，如果把计算机程序（代码）比作是魔法师的咒语，那么计算过程(process)则意指咒语发生作用的过程。

> Procedure 则更偏向于具体的概念，它更多是指我们通过程序设计、由具体语法定义所产生的，指定计算机去完成的一系列指令，更像是魔法师真正念动的咒语。

> 因此递归计算过程(recursive process)是指背后的计算过程是递归的，呈现出线展开后缩减的“形状”；而递归过程(recursive procedure)则指其语法形式，**例如上例中第二种算法实际上一个线性迭代的计算过程，但它却是通过递归过程的语法来实现的**。

之所以需要着重区分计算过程(process)与过程(procedure)，是因为在许多其它编程语言（如C）的设计中，即使描述的计算过程是迭代的，如果其实现过程是迭代过程（如上例中第二种方法），那么也会像递归计算过程一样消耗等比例的资源（像第一种方法一样），从而不得不借助于一些特殊的语法结构如`while`等来定义迭代计算过程，而Scheme没有这一缺陷，因为它通过**尾递归(tail-recursive)**的方法能够通过递归过程，只消耗常量的资源，完成迭代计算过程。

### 1.2.2 树形递归
    
    #lang racket
    ; 树形递归
    (define (fib n)
      (cond ((= n 0) 0)
        ((= n 1) 1)
        (else (+ (fib (- n 1))
                 (fib (- n 2))))))

计算过程所需的步骤数量随着`n`的增长呈指数型增长，即与树形递归中所有节点数量成正比；而计算过程消耗的资源则与树形递归的最大深度(`n`)成正比，因为在计算过程中的每一个时间点，只需要追踪所在节点之上的计算步骤。

`(fib n)`函数可以通过缓存`(fib (- n 1))`与`(fib (- n 2))`的值，用迭代计算过程完成：
   
    ; 迭代计算过程
    (define (fib-iter a b n)
      (if (= n 0)
          b
          (fib-iter (+ a b) a (- n 1))))

    (define (fib-2 n)
      (fib-iter 1 0 n))

#### 实例：零钱兑换可以有多少种方法？

将价值为a的金额用n种类型的硬币兑换，可以有多少种兑换方式（设共有N种）？

    N = (+ (不用第一种类型的硬币的兑换方法数量，M1) (至少使用一次第一种类型的硬币的兑换方法数量，M2))
    ;其中
    M2 = (从a中减去第一种硬币的面值d1后，n种类型的硬币兑换方法数量)
    ; M2的意义在于，既然至少使用了一次d1，其结果与从a中去掉d1的面值，重新考虑原来的问题，答案是一致的，这就是潜在的递归过程。

考虑一些边界情况：

    1. if a == 0, return 1;
    2. if a < 0, return 0;
    3. if n == 0, return 0.

示例代码：

    #lang racket
    (define (count-change amount)
      (cc amount 5)); 5 中硬币
    ;各种硬币的面值，从小到大排列
    ;？乱序排列是否可以？
    ; 可以。因为算法中没有涉及“第一种”硬币的面值。
    (define (first-denomination k)
      (cond ((= k 1) 1)
            ((= k 2) 50)
            ((= k 3) 10)
            ((= k 4) 25)
            ((= k 5) 5)))
    
    (define (cc amount n)
      (cond ((= amount 0) 1)
            ((or (< amount 0) (= n 0)) 0)
            (else (+
                   (cc amount (- n 1))
                   (cc (- amount (first-denomination n)) n)))))

这一树形递归计算过程相比fib函数更难转换成迭代计算过程，这样的树形递归计算过程执行效率更低但是却更容易被理解，因此人们总是希望能有一个聪明的编译器，可以将这些已于编写、理解的树形递归过程转换成计算效率更高的计算过程。

### 1.2.3 增长的阶

通过**增长的阶(order of growth)**可以粗略地表示随着输入（大小记为`n`）的增加，计算过程所需要消耗的资源数量，记为`R(n)`，`R(n)`增长的阶为`Θ(f(n))`，即：`R(n) = Θ(f(n))`，其含义为**存在常量`k1 & k2`，在`n`足够大的条件下，能够保证`k1*f(n) <= R(n) <= k2*f(n)`**。

### 1.2.4 求幂运算

    #lang racket
    ; 求幂
    ; 1. 线性递归的计算过程(linear recursive process)
    ; 步数增长的阶theta(n)，空间增长的阶theta(n)
    (define (expt-lrp b n)
      (if (= n 0)
          1
          (* b(expt-lrp b (- n 1)))))
    
    ; 2. 线性迭代的计算过程(linear iteration process)
    ; 步数增长的阶theta(n)，空间增长的阶theta(1)
    (define (expt-iter b n prod)
      (if (= n 0)
          prod
          (expt-iter b (- n 1) (* b prod))))
    (define (expt-lip b n)
      (expt-iter b n 1))
    
    ; 3. fast-expt
    ; 如果n是偶数，(expt b n) = (square (expt b (/ n 2)))
    ; 如果n是奇数，(expt b n) = (* b (expt b (- n 1)))
    ; 这样一来就变成一个非线性的递归计算过程
    ; 步数增长的阶为theta(log n) = 将n每次对折(或-1)，需要多少步降至0？
    (define (square x)
      (* x x))
    (define (fast-expt b n)
      (cond ((= n 0) 1)
            ((even? n) (square (fast-expt b (/ n 2))))
            (else (* b (fast-expt b (- n 1))))))
    
### 1.2.5 最大公约数(Greatest Common Divisors)

欧几里德算法（又称辗转相除法）：

    GCD(a, b) = GCD(b, a mod b)
    #lang racket
    (define (gcd a b)
      (if (= b 0)
          a
          (gcd b (remainder a b))))

### 1.2.6