#P74-P106

## 1.3 用高阶函数抽象(Formulating Abstractions with Higher-order Procedures)

对复合操作的抽象让我们可以通过对命名的方式，直接对这些抽象过程进行操作。

```racket
(define (cube x) (* x x x))
```

可以操作其它过程（把过程当做**参数或返回值**）的过程，称为**高阶过程(higher-order procedures)**。

### 1.3.1 过程作为参数

考虑如下过程：

```racket
#lang racket

; a 累加至 b
(define (sum-int a b)
  (if (> a b)
      0
      (+ a (sum-int (+ a 1) b))))

; a 立方累加至 b 立方
(define (cube a) (* a a a))
(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a)
         (sum-cubes (+ a 1) b))))

; pi / 8 = 1 / (1 * 3) + 1 / (5 * 7) + 1 / (9 * 11) ...
(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2)))
         (pi-sum (+ a 4) b))))
```

上述过程模式（pattern）相似，只是参数 a 累计的方法不同。通过如下模板，填入相应的过程产生相应的结果：

```racket
 #|
 #(define (<name> a b)
 # (if (> a b)
 #    0
 #    (+ (<term> a)
 #       (<name> (<next> a) b))))
|#

(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))
(define (inc n) (+ n 1))
(define (sum-cubes-pat a b)
  (sum cube a inc b))
(define (id x) x)
(define (sum-integers-pat a b)
  (sum id a inc b))
(define (pi-sum-pat a b)
  (define (pi-term x)
    (/ 1.0 (* x (+ x 2))))
  (define (pi-next x) (+ x 4))
  (sum pi-term a pi-next b))
```

### 1.3.2 用 Lambda 构建过程